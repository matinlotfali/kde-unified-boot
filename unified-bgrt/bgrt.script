splash_image = Image("image.png");
splash_sprite = Sprite(splash_image);
width = Window.GetWidth();
height = Window.GetHeight();
x = width/2 - splash_image.GetWidth()/2;
y = height/2 - splash_image.GetHeight()/2;
splash_sprite.SetPosition(x, y, 80);


#----------------------------------------- Dialogue --------------------------------

status = "normal";

fun dialog_setup()
  {
    local.box;
    local.lock;
    local.entry;
    
    box.image = Image("/usr/share/plymouth/themes/spinner/box.png");
    lock.image = Image("/usr/share/plymouth/themes/spinner/lock.png");
    entry.image = Image("/usr/share/plymouth/themes/spinner/entry.png");
    
    box.sprite = Sprite(box.image);
    box.x = Window.GetWidth()  * 0.50 - box.image.GetWidth ()/2;
    box.y = Window.GetHeight() * 0.75 - box.image.GetHeight()/2;
    box.z = 10000;
    box.sprite.SetPosition(box.x, box.y, box.z);
    
    lock.sprite = Sprite(lock.image);
    lock.x = box.x + box.image.GetWidth()/2 - (lock.image.GetWidth() + entry.image.GetWidth()) / 2;
    lock.y = box.y + box.image.GetHeight()/2 - lock.image.GetHeight()/2;
    lock.z = box.z + 1;
    lock.sprite.SetPosition(lock.x, lock.y, lock.z);
    
    entry.sprite = Sprite(entry.image);
    entry.x = lock.x + lock.image.GetWidth();
    entry.y = box.y + box.image.GetHeight()/2 - entry.image.GetHeight()/2;
    entry.z = box.z + 1;
    entry.sprite.SetPosition(entry.x, entry.y, entry.z);
    
    global.dialog.box = box;
    global.dialog.lock = lock;
    global.dialog.entry = entry;
    global.dialog.bullet_image = Image("bullet.png");
    dialog_opacity (1);
  }
    
fun dialog_opacity(opacity)
  {
    dialog.box.sprite.SetOpacity (opacity);
    dialog.lock.sprite.SetOpacity (opacity);
    dialog.entry.sprite.SetOpacity (opacity);
    for (index = 0; dialog.bullet[index]; index++)
      {
        dialog.bullet[index].sprite.SetOpacity(opacity);
      }
  }

fun display_normal_callback ()
  {
    global.status = "normal";
    if (global.dialog)
      dialog_opacity (0);
  }

fun display_password_callback (prompt, bullets)
  {
    global.status = "password";
    if (!global.dialog)
	  dialog_setup();
    else
	  dialog_opacity(1);
    for (index = 0; dialog.bullet[index] || index < bullets; index++)
      {
        if (!dialog.bullet[index])
          {
            dialog.bullet[index].sprite = Sprite(dialog.bullet_image);
            dialog.bullet[index].x = dialog.entry.x + index * dialog.bullet_image.GetWidth();
            dialog.bullet[index].y = dialog.entry.y + dialog.entry.image.GetHeight() / 2 - dialog.bullet_image.GetHeight() / 2;
            dialog.bullet[index].z = dialog.entry.z + 1;
            dialog.bullet[index].sprite.SetPosition(dialog.bullet[index].x, dialog.bullet[index].y, dialog.bullet[index].z);
          }
        if (index < bullets)
          dialog.bullet[index].sprite.SetOpacity(1);
        else
          dialog.bullet[index].sprite.SetOpacity(0);
      }
  }

Plymouth.SetDisplayNormalFunction(display_normal_callback);
Plymouth.SetDisplayPasswordFunction(display_password_callback);

#----------------------------------------- Progress Bar --------------------------------

if (Plymouth.GetMode () != "shutdown")
{
    progress_bar.image = Image("progress-0.png");
    progress_bar.sprite = Sprite(progress_bar.image);
    progress_bar.x = width/2 - progress_bar.image.GetWidth()  / 2;
    progress_bar.y = height*2/3;
    progress_bar.sprite.SetPosition(progress_bar.x, progress_bar.y, 100);

    fun progress_callback (duration, progress)
    {
        progress_bar.image = Image("progress-"+Math.Int(127*progress)+".png");
        progress_bar.sprite.SetImage (progress_bar.image);
    }

    Plymouth.SetBootProgressFunction(progress_callback);
}

#----------------------------------------- Message --------------------------------

message_sprite = Sprite();
message_sprite.SetPosition(10, 10, 1);

fun message_callback (text)
{
  my_image = Image.Text(text, 1, 1, 1);
  message_sprite.SetImage(my_image);
}

Plymouth.SetMessageFunction(message_callback);
